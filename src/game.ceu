#include "c.ceu"
#include "sdl/sdl.ceu"

#define WINDOW_W 800
#define WINDOW_H 576

#define GLYPH_W 48
#define GLYPH_H 64

var&? SDL_Init sdl =
    spawn SDL_Init("Gravity Platformer", WINDOW_W, WINDOW_H, SDL_Color(0xFF,0xFF,0xFF,0xFF));

watching sdl do
    var int highScore = 0;
    loop do
        var int runSpeed = 200;
        var int score = 1;
        event void gameOver;
        event void scoreUpdate;

        #include "utilities.ceu"
        #include "platform.ceu"        
        #include "player.ceu"  

        var int minimumSize = 8;
        var int yVariation = 2;

        code/tight WidthToMS (var int w) -> int do
            escape ((1000 * w)/outer.runSpeed);
        end

        native/pre do
            ##include <time.h>
        end

        native _log10;
        _srand(_time(null));

        var int side = -1;

        var int x = 0;
        var int y = 10;
        var int w = 20;
        
        var int scorelength = _log10(score) + 1;

        var& SDL_Open_Image img_scorefont =
        spawn SDL_Open_Image(&sdl!.ren, "res/font.png");
        
        spawn Platform(x,y * BLOCK_SIZE,w,1) in platforms;
        var& Player player = spawn Player(72, (y-0.5) * BLOCK_SIZE);
        var int t = call WidthToMS((w * BLOCK_SIZE) - WINDOW_W);
        await (t)ms;

        par/or do
            loop do
                w = minimumSize + _rand()%10;
                if (_rand()%1000 > 950) then
                    w = w + 20;
                end
                x = WINDOW_W;
                y = (5 + side*(2+_rand()%yVariation)) * BLOCK_SIZE;
                spawn Platform(x,y,w,side) in platforms;
                t = call WidthToMS(w * BLOCK_SIZE);
                //_printf("Y=%d, To wait: %d ms\n", y, t);
                await (t)ms;
                side = side * -1;
                runSpeed = runSpeed + 10;
                player.fallSpeed = player.fallSpeed + 20;
                if (runSpeed%120 == 0 and minimumSize > 3) then
                    minimumSize = minimumSize-1;
                end
                if (runSpeed%190 == 0 and yVariation < 3) then
                    yVariation = yVariation+1;
                end
                score = score+1;
                scorelength = _log10(score) + 1;
            end
        with
            every SDL_REDRAW do
                var int i;
                var int scoreRem = score;
                var int scoreDigit;
                var int baseX = ((WINDOW_W>>1) + (((scorelength-1) as float)/2.0 - 0.5) * (GLYPH_W)) as int ;
                loop i in [0 -> scorelength[ do
                    scoreDigit = scoreRem%10;
                    scoreRem = scoreRem/10;
                    var SDL_Rect score_rect = val SDL_Rect(baseX - (GLYPH_W*i), (WINDOW_H)-GLYPH_H, GLYPH_W, GLYPH_H);
                    var SDL_Rect clip_rect = val SDL_Rect(scoreDigit * GLYPH_W, 0, GLYPH_W, GLYPH_H);
                    _SDL_RenderCopy(&&sdl!.ren, &&img_scorefont.tex.tex, &&clip_rect as _SDL_Rect&&, &&score_rect as _SDL_Rect&&);
                end
            end
        with
            await gameOver;
            if (score > highScore) then
                _printf("=================================\n");
                _printf("A NEW HIGHSCORE! YOU SCORED %d.\n", score);
                _printf("=================================\n\n");
                highScore = score;
            end
        end
    end
end

escape 0;
